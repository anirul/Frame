cmake_minimum_required(VERSION 3.14)

project(frame DESCRIPTION "Frame Graphic Library." LANGUAGES CXX)

# To put executables next to the runtime libraries generated by conan.
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)

# For fPIC / position independent code.
set(CMAKE_POSITION_INDEPENDENT_CODE ON)

# C++ standard version.
set(CMAKE_CXX_STANDARD 23)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# Adding subfolder property.
set_property(GLOBAL PROPERTY USE_FOLDERS ON)

# Enable testing.
set(CMAKE_CTEST_ARGUMENTS "--output-on-failure")
enable_testing()

# External packages.
find_package(absl CONFIG REQUIRED)
find_package(glad CONFIG REQUIRED)
find_package(glm CONFIG REQUIRED)
find_package(GTest CONFIG REQUIRED)
find_package(protobuf CONFIG REQUIRED)
find_package(SDL3 CONFIG REQUIRED)
find_package(spdlog CONFIG REQUIRED)
find_package(tinyobjloader CONFIG REQUIRED)
find_package(imgui CONFIG REQUIRED)
find_package(shaderc CONFIG QUIET)
if(NOT shaderc_FOUND)
  find_package(unofficial-shaderc CONFIG REQUIRED)
  if(NOT TARGET shaderc::shaderc_combined)
    if(TARGET unofficial::shaderc::shaderc_combined)
      add_library(shaderc::shaderc_combined INTERFACE IMPORTED)
      target_link_libraries(shaderc::shaderc_combined INTERFACE unofficial::shaderc::shaderc_combined)
    elseif(TARGET unofficial::shaderc::shaderc)
      add_library(shaderc::shaderc_combined INTERFACE IMPORTED)
      target_link_libraries(shaderc::shaderc_combined INTERFACE unofficial::shaderc::shaderc)
    endif()
  endif()
  if(NOT TARGET shaderc::shaderc_shared AND TARGET unofficial::shaderc::shaderc_shared)
    add_library(shaderc::shaderc_shared INTERFACE IMPORTED)
    target_link_libraries(shaderc::shaderc_shared INTERFACE unofficial::shaderc::shaderc_shared)
  endif()
endif()
find_package(Vulkan REQUIRED)
if(UNIX AND NOT APPLE AND TARGET Vulkan::Vulkan)
  get_target_property(_frame_vulkan_lib Vulkan::Vulkan IMPORTED_LOCATION)
  if(_frame_vulkan_lib AND NOT EXISTS "${_frame_vulkan_lib}")
    find_library(_frame_vulkan_fallback NAMES vulkan)
    if(_frame_vulkan_fallback)
      set_property(TARGET Vulkan::Vulkan PROPERTY IMPORTED_LOCATION "${_frame_vulkan_fallback}")
    endif()
  endif()
  unset(_frame_vulkan_lib)
  unset(_frame_vulkan_fallback)
endif()

if(UNIX AND NOT APPLE)
  set(FRAME_LINK_GROUP_BEGIN "-Wl,--start-group")
  set(FRAME_LINK_GROUP_END "-Wl,--end-group")
endif()

set(FRAME_CORE_LIBS
  Frame
  FrameFile
  FrameJson
  FrameOpenGL
  FrameOpenGLFile
  FrameOpenGLGui
  FrameGui
  FrameProto
)

set(FRAME_VULKAN_LIBS
  FrameVulkan
)

include(cmake/AddImNodeFlow.cmake)

# Sources.
add_subdirectory(external/ImGuiColorTextEdit)
set_property(TARGET ImGuiColorTextEdit PROPERTY FOLDER "external/ImGuiColorTextEditor")
set_property(TARGET ImNodeFlow PROPERTY FOLDER "external/ImNodeFlow")
add_subdirectory(asset/shader/opengl)
add_subdirectory(asset/shader/vulkan)
add_subdirectory(asset/json)
add_subdirectory(src/frame)
add_subdirectory(editor)
add_subdirectory(examples)
add_subdirectory(tests/frame)
