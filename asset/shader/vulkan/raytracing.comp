#version 450

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(set = 0, binding = 0, rgba16f) uniform writeonly image2D output_image;
layout(set = 0, binding = 1) uniform sampler2D raytrace_output;
layout(set = 0, binding = 2) uniform sampler2D albedo_texture;
layout(set = 0, binding = 3) uniform sampler2D normal_texture;
layout(set = 0, binding = 4) uniform sampler2D roughness_texture;
layout(set = 0, binding = 5) uniform sampler2D metallic_texture;
layout(set = 0, binding = 6) uniform sampler2D ao_texture;
layout(set = 0, binding = 7) uniform samplerCube skybox_env;

struct Vertex
{
    vec4 position; // xyz + pad
    vec4 normal;   // xyz + pad
    vec4 uv_pad;   // uv + pad
};

struct Triangle
{
    Vertex v0;
    Vertex v1;
    Vertex v2;
};

struct BvhNode
{
    vec4 min;
    vec4 max;
    int left;
    int right;
    int first_triangle;
    int triangle_count;
};

layout(std430, set = 0, binding = 8) buffer TriangleBuffer
{
    Triangle triangles[];
};

layout(std430, set = 0, binding = 9) buffer BvhBuffer
{
    BvhNode nodes[];
};

layout(set = 0, binding = 10) uniform UniformBlock
{
    mat4 projection;
    mat4 view;
    mat4 projection_inv;
    mat4 view_inv;
    mat4 model;
    mat4 model_inv;
    vec4 camera_position;
    vec4 light_dir;
    vec4 light_color;
    vec4 time_s;
} ubo;

// ----------------------------------------------------------------------------
float DistributionGGX(vec3 N, vec3 H, float roughness)
{
    float a = roughness * roughness;
    float a2 = a * a;
    float NdotH = max(dot(N, H), 0.0);
    float NdotH2 = NdotH * NdotH;
    float denom = (NdotH2 * (a2 - 1.0) + 1.0);
    return a2 / (3.14159265 * denom * denom);
}

float GeometrySchlickGGX(float NdotV, float roughness)
{
    float r = roughness + 1.0;
    float k = (r * r) / 8.0;
    return NdotV / (NdotV * (1.0 - k) + k);
}

float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx1 = GeometrySchlickGGX(NdotV, roughness);
    float ggx2 = GeometrySchlickGGX(NdotL, roughness);
    return ggx1 * ggx2;
}

vec3 fresnelSchlick(float cosTheta, vec3 F0)
{
    return F0 + (1.0 - F0) * pow(1.0 - cosTheta, 5.0);
}

bool rayTriangleIntersect(
    const vec3 ray_origin,
    const vec3 ray_direction,
    const Triangle triangle,
    out float out_t,
    out vec2 out_bary)
{
    const float EPSILON = 0.0000001;
    vec3 edge1 = triangle.v1.position.xyz - triangle.v0.position.xyz;
    vec3 edge2 = triangle.v2.position.xyz - triangle.v0.position.xyz;
    vec3 h = cross(ray_direction, edge2);
    float a = dot(edge1, h);
    if (a > -EPSILON && a < EPSILON)
        return false; // Ray is parallel to triangle.
    float f = 1.0 / a;
    vec3 s = ray_origin - triangle.v0.position.xyz;
    float u = f * dot(s, h);
    if (u < 0.0 || u > 1.0)
        return false;
    vec3 q = cross(s, edge1);
    float v = f * dot(ray_direction, q);
    if (v < 0.0 || u + v > 1.0)
        return false;
    float t = f * dot(edge2, q);
    if (t > EPSILON)
    {
        out_t = t;
        out_bary = vec2(u, v);
        return true;
    }
    return false;
}

bool rayAabbIntersect(
    const vec3 ray_origin, const vec3 inv_ray_dir, const BvhNode node)
{
    vec3 t0 = (node.min.xyz - ray_origin) * inv_ray_dir;
    vec3 t1 = (node.max.xyz - ray_origin) * inv_ray_dir;
    vec3 tmin = min(t0, t1);
    vec3 tmax = max(t0, t1);
    float t_enter = max(max(tmin.x, tmin.y), tmin.z);
    float t_exit = min(min(tmax.x, tmax.y), tmax.z);
    return t_exit >= max(t_enter, 0.0);
}

bool traverseBVH(
    const vec3 ray_origin,
    const vec3 ray_dir,
    out float out_t,
    out vec2 out_bary,
    out int out_tri)
{
    vec3 inv_ray_dir = 1.0 / ray_dir;
    int stack[64];
    int stack_ptr = 0;
    stack[stack_ptr++] = 0;
    out_t = 1e20;
    out_tri = -1;
    bool hit = false;
    while (stack_ptr > 0)
    {
        int node_index = stack[--stack_ptr];
        BvhNode node = nodes[node_index];
        if (!rayAabbIntersect(ray_origin, inv_ray_dir, node))
            continue;
        if (node.triangle_count > 0)
        {
            for (int i = 0; i < node.triangle_count; ++i)
            {
                int tri_index = node.first_triangle + i;
                float t;
                vec2 bary;
                if (rayTriangleIntersect(
                        ray_origin,
                        ray_dir,
                        triangles[tri_index],
                        t,
                        bary) &&
                    t < out_t)
                {
                    out_t = t;
                    out_bary = bary;
                    out_tri = tri_index;
                    hit = true;
                }
            }
        }
        else
        {
            if (node.left >= 0)
                stack[stack_ptr++] = node.left;
            if (node.right >= 0)
                stack[stack_ptr++] = node.right;
        }
    }
    return hit;
}

void main()
{
    ivec2 pixel = ivec2(gl_GlobalInvocationID.xy);
    ivec2 size = imageSize(output_image);
    if (pixel.x >= size.x || pixel.y >= size.y)
        return;

    vec2 uv = (vec2(pixel) + vec2(0.5)) / vec2(size);
    vec2 ndc = uv * 2.0 - 1.0;
    vec4 clip_pos = vec4(ndc, -1.0, 1.0);
    vec4 view_pos = ubo.projection_inv * clip_pos;
    view_pos = vec4(view_pos.xy, -1.0, 0.0);
    vec3 ray_dir_world = normalize((ubo.view_inv * view_pos).xyz);

    mat4 inv_model4 = ubo.model_inv;
    mat3 model_inv3 = mat3(inv_model4);
    float inv_det = abs(determinant(model_inv3));
    if (inv_det < 1e-8)
    {
        inv_model4 = inverse(ubo.model);
        model_inv3 = mat3(inv_model4);
    }
    mat3 normal_matrix = transpose(model_inv3);
    vec3 ray_origin = (inv_model4 * vec4(ubo.camera_position.xyz, 1.0)).xyz;
    vec3 ray_dir = normalize(model_inv3 * ray_dir_world);

    float closest_t;
    vec2 hit_bary;
    int tri_index;
    bool hit = traverseBVH(ray_origin, ray_dir, closest_t, hit_bary, tri_index);
    vec4 final_color = vec4(0.0);
    if (hit)
    {
        Triangle tri = triangles[tri_index];
        vec3 p0 = tri.v0.position.xyz;
        vec3 p1 = tri.v1.position.xyz;
        vec3 p2 = tri.v2.position.xyz;
        float w = 1.0 - hit_bary.x - hit_bary.y;
        vec3 hit_normal_model =
            normalize(w * tri.v0.normal.xyz + hit_bary.x * tri.v1.normal.xyz +
                      hit_bary.y * tri.v2.normal.xyz);
        vec2 hit_uv = w * tri.v0.uv_pad.xy + hit_bary.x * tri.v1.uv_pad.xy +
                      hit_bary.y * tri.v2.uv_pad.xy;
        vec3 hit_position = ray_origin + ray_dir * closest_t;
        vec3 hit_normal_world = normalize(normal_matrix * hit_normal_model);
        vec3 hit_position_world =
            (ubo.model * vec4(hit_position, 1.0)).xyz;

        vec3 albedo = pow(texture(albedo_texture, hit_uv).rgb, vec3(2.2));
        vec3 normal_tex = texture(normal_texture, hit_uv).xyz * 2.0 - 1.0;
        normal_tex = normalize(normal_matrix * normal_tex);
        float roughness = texture(roughness_texture, hit_uv).r;
        float metallic = texture(metallic_texture, hit_uv).r;
        float ao = texture(ao_texture, hit_uv).r;

        vec3 N = normalize(hit_normal_world + normal_tex);
        vec3 V = normalize(ubo.camera_position.xyz - hit_position_world);
        vec3 L = normalize(-ubo.light_dir.xyz);
        vec3 H = normalize(V + L);

        vec3 F0 = vec3(0.04);
        F0 = mix(F0, albedo, metallic);
        float NDF = DistributionGGX(N, H, roughness);
        float G = GeometrySmith(N, V, L, roughness);
        vec3 F = fresnelSchlick(max(dot(H, V), 0.0), F0);

        vec3 nominator = NDF * G * F;
        float denom = 4.0 * max(dot(N, V), 0.0) * max(dot(N, L), 0.0) + 0.001;
        vec3 specular = nominator / denom;

        vec3 kS = F;
        vec3 kD = vec3(1.0) - kS;
        kD *= 1.0 - metallic;

        float NdotL = max(dot(N, L), 0.0);
        vec3 irradiance = vec3(0.0);
        vec3 diffuse = irradiance * albedo;
        vec3 ambient = (kD * diffuse + specular) * ao;

        vec3 Lo = (kD * albedo / 3.14159265 + specular) *
                  ubo.light_color.rgb * NdotL;
        vec3 color = ambient + Lo;
        color = color / (color + vec3(1.0));
        color = pow(color, vec3(1.0 / 2.2));
        final_color = vec4(color, 1.0);
    }
    else
    {
        // Miss: sample skybox environment using world-space ray.
        vec3 sky = texture(skybox_env, ray_dir_world).rgb;
        sky = pow(sky, vec3(1.0 / 2.2));
        final_color = vec4(sky, 1.0);
    }

    imageStore(output_image, pixel, final_color);
}
